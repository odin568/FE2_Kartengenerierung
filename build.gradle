plugins {
	id 'org.springframework.boot' version '2.4.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'idea'
}

sourceCompatibility = JavaVersion.VERSION_11
// GitHub-Version >> Docker Version >> Fallback
if (System.getenv("RELEASE_VERSION")) {
	version = System.getenv("RELEASE_VERSION")
	println("Building version $version from environment variable")
}
else if (project.hasProperty("RELEASE_VERSION") && project.getProperty("RELEASE_VERSION") && !project.getProperty("RELEASE_VERSION").allWhitespace) {
	version = project.getProperty("RELEASE_VERSION")
	println("Building version $version from property")
}
else {
	version = "SNAPSHOT"
	println("Building version $version from fallback")
}

// Omit version number in artifacts for simpler releases
bootJar.archiveFileName = "FE2_Kartengenerierung.jar"

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'com.google.maps:google-maps-services:0.18.0'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
	useJUnitPlatform()
}

springBoot {
	buildInfo()
}

// Build zip containing WinSW ServiceWrapper and Jar File
task packageDistribution(type: Zip) {
	archiveFileName = "FE2_Kartengenerierung-${archiveVersion.get()}.zip"
	destinationDirectory = file("$buildDir/dist")

	from "$buildDir/../deploy/winsw", "$buildDir/libs"
	dependsOn(assemble)
}
// Build docker-compose.yml with correct version
task prepareDockerCompose(type: Copy) {
	from "$buildDir/../deploy/docker/docker-compose.yml"
	into "$buildDir/dist"
	filter { line -> line.replaceAll('latest', version) }
}

build.dependsOn(prepareDockerCompose)
build.dependsOn(packageDistribution)
